plugins {
    id "org.sonarqube" version "3.0"
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = '0.8.5'
    }
}

subprojects {
    apply plugin: 'java'

    group 'org.scrum.psd.battleship'
    version '1.0.0-SNAPSHOT'

    sourceCompatibility = 1.8

    dependencies {
        testImplementation 'org.slf4j:slf4j-api:1.7.30',
                'io.cucumber:cucumber-junit:6.1.2',
                'io.cucumber:cucumber-java:6.1.2',
                'org.jacoco:org.jacoco.ant:0.8.5', 
                'org.junit.jupiter:junit-jupiter-api:5.6.2'

        testCompileOnly 'junit:junit:4.12'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2',
                'org.junit.vintage:junit-vintage-engine:5.6.2'

    }

    test {
        useJUnitPlatform()

        jacoco {
            includes = ['org.scrum.psd.battleship.*']
        }
    }
}

task copyTestResults {
    doLast {
        copy {
            from fileTree('.').files
            into 'build/test-results'
            include 'TEST-*.xml'
        }
    }
}

task jacocoMerge(type: JacocoMerge) {
    executionData fileTree('.').include('**/build/jacoco/*.exec')
}
